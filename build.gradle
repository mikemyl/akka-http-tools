plugins {
    id 'maven-publish'
    id 'signing'
}

apply plugin: 'scala'
apply plugin: 'java-library'

group 'com.sbuslab'

repositories {
    maven { url 'https://oss.sonatype.org/content/repositories/public' }
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'akka-http-tools'
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'akka-http-tools'
                description = 'Tools for akka-http (marshaling, error handling, websockets, sbus support)'
                url = 'https://github.com/sbuslab/akka-http-tools'
                inceptionYear = '2018'

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'kulikovd'
                        name = 'Dmitry Kulikov'
                        email = 'kulikov.dm@gmail.com'
                    }
                }

                scm {
                    url = 'https://github.com/sbuslab/akka-http-tools'
                    connection = 'scm:https://github.com/sbuslab/akka-http-tools.git'
                    developerConnection = 'scm:git@github.com:sbuslab/akka-http-tools.git'
                }
            }
        }
    }

    if (project.findProperty('publishSonatype')) {
        repositories {
            maven {
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"

                credentials {
                    username = project.findProperty('nexusUsername') ?: ''
                    password = project.findProperty('nexusPassword') ?: ''
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    implementation 'com.sbuslab:utils:[1.4.0,1.5.0)'

    api 'ch.megard:akka-http-cors_2.12:1.1.3'

    api 'com.typesafe.akka:akka-http_2.12:10.2.9'
    implementation 'com.typesafe.akka:akka-http-jackson_2.12:10.2.9'
    api 'com.typesafe.akka:akka-stream_2.12:2.6.19'
}
